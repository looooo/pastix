extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> c d s
   *
   */
#include <parsec.h>
#include <parsec/data_distribution.h>
#include <parsec/private_mempool.h>
#include "common.h"
#include "solver.h"
#include "pastix_zcores.h"
#include "sopalin_data.h"

%}

/* Globals
 */
descA        [type = "sparse_matrix_desc_t *" ]
sopalin_data [type = "sopalin_data_t *" ]

datacode  [type = "SolverMatrix*"         hidden = on default = "(sopalin_data->solvmtx)"       ]
cblknbr   [type = "pastix_int_t"          hidden = on default = "(datacode->cblknbr - 1)"       ]
bloknbr   [type = "pastix_int_t"          hidden = on default = "(datacode->bloknbr - 2)"       ]
lowrank   [type = "pastix_lr_t"           hidden = on default = "(sopalin_data->solvmtx->lowrank)"]

p_work    [type = "parsec_memory_pool_t *"]

/**************************************************
 *                   GETRF                        *
 * panel factorization: do trf of diagonal and    *
 *                    : trsm on off-diagonal      *
 **************************************************/
GETRF(k) [high_priority = on]

// Execution space
k = 0 .. cblknbr

browk0    = %{ SolverCblk *cblk = datacode->cblktab + k;     return cblk->brownum; %}
browk1    = %{ SolverCblk *cblk = datacode->cblktab + k + 1; return cblk->brownum; %}
lastbrow  = %{ if ( browk0 == browk1 ) return 0; else return datacode->browtab[ browk1 - 1 ]; %}
firstblok = %{ SolverCblk *cblk = datacode->cblktab + k;     return cblk->fblokptr - datacode->bloktab + 1; %}
lastblok  = %{ SolverCblk *cblk = datacode->cblktab + k + 1; return cblk->fblokptr - datacode->bloktab - 1; %}

// Parallel partitioning
:descA(0, k, 0)

// Parameters
/* C is A(k) if it's a leaf or get the cblk from the last update */
RW L <- ( browk0 == browk1 ) ? descA(0, k, 0) : C GEMM1D( lastbrow, 0 )
     -> Al GEMM1D(firstblok .. lastblok, 0 .. 1)
     -> descA(0, k, 0)

RW U <- ( browk0 == browk1 ) ? descA(1, k, 0) : C GEMM1D( lastbrow, 1 )
     -> Au GEMM1D(firstblok .. lastblok, 0 .. 1)
     -> descA(1, k, 0)

; %{ return cblknbr - k; %}

BODY
{
    SolverCblk *cblk = datacode->cblktab + k;
    core_zgetrfsp1d_panel(cblk, L, U, sopalin_data->diagthreshold, &lowrank );
}
END

/**
 *       GEMM
 *
 * To have a contiguous range of GEMM to release in the potrf, they are numbered
 * with the indexes of the off-diagonal blocks, diagonal block included.
 * Thus, the diagonal block tasks which doesn't perfom computations are used as
 * DATA_IN tasks. This is mandatory when using the GPU, due to the versioning
 * bumped by the cpu version of the diagonal block that coccurs when computing
 * the diagonal blocks.
 *
 * For all off-diagonal blocks, it updates the trailing matrix with the panel
 * k-th block updating corresponding.
 *
 */
GEMM1D(s, bloknum)

// Execution space
s       = 0 .. 1
bloknum = 1 .. bloknbr

lcblknm = %{ SolverBlok *blok = datacode->bloktab + bloknum;     return blok->lcblknm; %}
fcblknm = %{ SolverBlok *blok = datacode->bloktab + bloknum;     return blok->fcblknm; %}
first   = %{ SolverCblk *cblk = datacode->cblktab + fcblknm;     return cblk->brownum; %}
last    = %{ SolverCblk *cblk = datacode->cblktab + fcblknm + 1; return cblk->brownum - 1; %}

brownum = %{ return datacode->bloktab[bloknum].browind; %} /* -1 if diagonal block */
prev    = %{
    assert( first >= 0 );
    /**
     * If bloknum is a diagonal block, or if it is the first one applied on C,
     * there is no previous
     */
    if ((brownum == -1) || (brownum == first) ) {
        return 0;
    }
    /**
     * Otherwise we return the previous block in the list of blocks facing fcblk
     */
    else {
        assert( brownum > first );
        return datacode->browtab[brownum-1];
    }
    %}
next    = %{
    /**
     * If we are on a diagonal blok, or if we are the last one, there is no next
     */
    if ((brownum == -1) || (brownum >= last) ){
        return 0;
    } else {
        return datacode->browtab[brownum+1];
    }
    %}

n  = %{ if (brownum == -1) return 0; else return blok_rownbr(datacode->bloktab + bloknum); %}
k  = %{ if (brownum == -1) return 0; else return cblk_colnbr(datacode->cblktab + lcblknm); %}
m  = %{ if (brownum == -1) return 0; else {
        if ((datacode->cblktab + lcblknm)->cblktype & CBLK_LAYOUT_2D) {
            return datacode->cblktab[lcblknm].stride - (datacode->bloktab[bloknum].coefind / k);
        } else {
            return datacode->cblktab[lcblknm].stride - datacode->bloktab[bloknum].coefind;
        }
    }%}

// Parallel partitioning
:descA(s, fcblknm, 0)

// Parameters
READ  Al <- (brownum != -1) ? L GETRF( lcblknm ) : NULL
READ  Au <- (brownum != -1) ? U GETRF( lcblknm ) : NULL

RW    C  <- (brownum == -1) ? NULL
         <- (brownum != -1) && (brownum == first) ? descA( s, fcblknm, 0 )
         <- (brownum != -1) && (brownum != first) ? C GEMM1D( s, prev )

         -> (brownum != -1) && (brownum == last) && (s == 0) ? L GETRF( fcblknm )
         -> (brownum != -1) && (brownum == last) && (s != 0) ? U GETRF( fcblknm )
         -> (brownum != -1) && (brownum != last)             ? C GEMM1D( s, next )

; %{ return cblknbr - ((fcblknm + lcblknm) / 2 ) + last - brownum; %}

BODY
{
    /* If diagonal block, we skip it */
    if (brownum != -1) {
        SolverCblk *lcblk = datacode->cblktab + lcblknm;
        SolverCblk *fcblk = datacode->cblktab + fcblknm;
        SolverBlok *blok  = datacode->bloktab + bloknum;
        pastix_complex64_t *work = NULL;

        /* Update */
        if ( blok+s < lcblk[1].fblokptr ) {
            //work = (pastix_complex64_t *)parsec_private_memory_pop( p_work );

            if (s == 0) {
                cpucblk_zgemmsp( PastixLCoef, PastixUCoef, PastixTrans,
                                 lcblk, blok, fcblk,
                                 Al, Au, C,
                                 work, &lowrank );
            }
            else {
                cpucblk_zgemmsp( PastixUCoef, PastixLCoef, PastixTrans,
                                 lcblk, blok, fcblk,
                                 Au, Al, C,
                                 work, &lowrank );
            }
            //parsec_private_memory_push( p_work, (void *)work );
        }
    }
}
END
