###
#
#  @copyright 2017      Bordeaux INP, CNRS (LaBRI UMR 5800), Inria,
#                       Univ. Bordeaux. All rights reserved.
#
#  @version 6.0.0
#  @author Mathieu Faverge
#  @date 2013-06-24
#
###

### CTest execution
set( PASTIX_DRIVERS
  analyze simple step-by-step schur personal) # reentrant fails with Scotch parser !
set( PASTIX_TESTS
  simple )
set( PASTIX_ARITHM
  s d c z)
set( PASTIX_SCHEDS
     0 1) # sequential, static
if (PASTIX_WITH_PARSEC)
  list( APPEND PASTIX_SCHEDS 2 ) # parsec
endif (PASTIX_WITH_PARSEC)
if (PASTIX_WITH_STARPU)
  list( APPEND PASTIX_SCHEDS 3 ) # starpu
endif (PASTIX_WITH_STARPU)

### All drivers with Laplacian and default parameters
foreach(example ${PASTIX_DRIVERS} )
  foreach(arithm ${PASTIX_ARITHM} )
    add_test(example_drv_${arithm}_${example} ./${example} -9 ${arithm}:10:10:10 -f 2) # LLt fails with negative pivot !
  endforeach()
endforeach()


### Factotype
foreach(example ${PASTIX_TESTS} )
  # RSA
  add_test(example_rsa_${example} ./${example} --rsa ${CMAKE_SOURCE_DIR}/test/matrix/small.rsa    -f 2) # LLt fails with negative pivot !
  # Matrix Market
  add_test(example_mm_${example}  ./${example} --mm  ${CMAKE_SOURCE_DIR}/test/matrix/young4c.mtx  -f 2) # LLt fails with negative pivot !
  # Harwell Boeing
  add_test(example_hb_${example}  ./${example} --hb  ${CMAKE_SOURCE_DIR}/test/matrix/orsirr.rua   -f 2)
  # Matrix Market - Hermitian
  add_test(example_mm2_${example} ./${example} --mm  ${CMAKE_SOURCE_DIR}/test/matrix/mhd1280b.mtx -f 2)
endforeach()

### Refinement
foreach(example ${PASTIX_TESTS} )
  add_test(example_cg_${example}       ./${example} --rsa ${CMAKE_SOURCE_DIR}/test/matrix/small.rsa  -f 2 -i iparm_refinement pastixrefinegmres) # LLt fails with negative pivot !
  add_test(example_gmres_${example}    ./${example} --hb  ${CMAKE_SOURCE_DIR}/test/matrix/orsirr.rua -f 2 -i iparm_refinement pastixrefinegmres)
  add_test(example_bicgstab_${example} ./${example} --hb  ${CMAKE_SOURCE_DIR}/test/matrix/orsirr.rua -f 2 -i iparm_refinement pastixrefinebicgstab)
endforeach()

### Scheduling
foreach(example ${PASTIX_TESTS} )
  foreach(scheduler ${PASTIX_SCHEDS} )
    foreach(arithm ${PASTIX_ARITHM} )
      add_test(example_sched${scheduler}_1d_${arithm}_${example}     ./${example} -9 ${arithm}:10:10:10 -s ${scheduler})
      if ((${scheduler} EQUAL 2) OR (${scheduler} EQUAL 3))
        # 2D
        add_test(example_sched${scheduler}_2d_${arithm}_${example}   ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_distribution_level 0 )
        # 1D/2D
        add_test(example_sched${scheduler}_1d2d_${arithm}_${example} ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_distribution_level 16)
      endif()
    endforeach()
  endforeach()
endforeach()

# Low Rank
foreach(example ${PASTIX_TESTS} )
  foreach(scheduler ${PASTIX_SCHEDS} )
    if (NOT (${scheduler} EQUAL 3) ) # no low-rank with StarPU
      foreach(arithm ${PASTIX_ARITHM} )
        # SVD Begin
        add_test(example_svdbegin_sched${scheduler}_${arithm}_${example}  ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_compress_when pastixcompresswhenbegin -i iparm_compress_method pastixcompressmethodsvd  -i iparm_compress_min_width 16 -i iparm_compress_min_height 16)
        # SVD End
        add_test(example_svdend_sched${scheduler}_${arithm}_${example}    ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_compress_when pastixcompresswhenend   -i iparm_compress_method pastixcompressmethodsvd  -i iparm_compress_min_width 16 -i iparm_compress_min_height 16)
        # RRQR Begin
        add_test(example_rrqrbegin_sched${scheduler}_${arithm}_${example} ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_compress_when pastixcompresswhenbegin -i iparm_compress_method pastixcompressmethodrrqr -i iparm_compress_min_width 16 -i iparm_compress_min_height 16)
        # RRQR End
        add_test(example_rrqrend_sched${scheduler}_${arithm}_${example}   ./${example} -9 ${arithm}:10:10:10 -s ${scheduler} -i iparm_compress_when pastixcompresswhenend   -i iparm_compress_method pastixcompressmethodrrqr -i iparm_compress_min_width 16 -i iparm_compress_min_height 16)
      endforeach()
    endif()
  endforeach()
endforeach()
